
/**
 * Copyright (C) 2017-2018 Jxpacket
 */

allprojects {

    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'com.jfrog.bintray'

    signing {
        required { gradle.taskGraph.hasTask('uploadArchives') || gradle.taskGraph.hasTask('bintrayUpload') }
        sign configurations.archives
    }

    if (!project.name.equals("jxpacket")) {
        artifacts {
            archives project.jar, project.sourcesJar, project.javadocJar
        }
    }

    uploadArchives {
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: defaultBlank({ sonatypeUser }), password: defaultBlank({ sonatypePass }))
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: defaultBlank({ sonatypeUser }), password: defaultBlank({ sonatypePass }))
            }
            pom.project pom_project
            pom.project {

                name = project.name

                if (project.description == null || project.description.isEmpty()) {
                    description = "${DESCRIPTION}"
                } else {
                    description = project.description
                }

                if (project.name.equals('jxpacket')) {
                    dependencyManagement {
                        dependencies bom_dependencies
                    }
                }

            }
        }
    }

    bintray {
        user = defaultBlank({ bintrayUser })
        key = defaultBlank({ bintrayPass })
        configurations = ['archives']
        dryRun = false
        publish = true
        override = true
        pkg {
            repo = 'maven'
            name = 'com.ardikars.jxpacket'
            desc = 'Jxpacket is a java library for network packet crafting.'
            licenses = ['GNU Lesser General Public License, Version 3.0']
            vcsUrl = 'https://github.com/jxnet/Jxpacket.git'
            version {
                name = project.version
                vcsTag = project.version
                gpg {
                    sign = true
                    passphrase = defaultBlank({ signing.password })
                }
                mavenCentralSync {
                    sync = false
                    user = defaultBlank({ sonatypeUser })
                    password = defaultBlank({ sonatypePass })
                    close = '1'
                }
            }
        }
    }

}
